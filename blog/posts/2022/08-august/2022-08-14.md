---
title: "Haskell `conduit` example app"
tags: ["Haskell", "Streaming"]
created_at: 2022-08-14
---

I was playing around with the [conduit](https://github.com/snoyberg/conduit#readme) streaming Haskell library. I wanted to create an example app which would stream a file containing numbers separated by a new line, it would do a mapping on each number (in my example I just double it, the actual arithmetic operation isn't important) and stream the result to an output file. The point is the whole program should be constant in memory even tho I process a file containing 1 million numbers.

For an introduction I recommend [this talk](https://www.youtube.com/watch?v=9957qVltU00) by Michael Snoyman who is the author of the library.

## The pipeline

The streaming pipeline itself is pretty simple. 

- We need to stream a file (using [sourceFile](https://hackage.haskell.org/package/conduit-1.3.4.1/docs/Data-Conduit-Combinators.html#v:sourceFile)) as `ByteString`.
- We aggregate each line (using [linesUnboundedAscii](https://hackage.haskell.org/package/conduit-1.3.4.1/docs/Data-Conduit-Combinators.html#v:linesUnboundedAscii)).
- In the downstream, convert the `ByteString` to `Int` (it is actually unsafe because I'm using the prelude's [read](https://hackage.haskell.org/package/base-4.17.0.0/docs/Prelude.html#v:read) function, don't do that in a production app!).
- Finally, the very next downstream does the arithmetic transformation and the result gets converted from `Int` back to the `ByteString`.
- The output is streamed to the output file (using [sinkFile](https://hackage.haskell.org/package/conduit-1.3.4.1/docs/Data-Conduit-Combinators.html#v:sinkFile)).

## Generate input file

```bash
python -c 'print("\n".join(list(map(str, range(0, 1_000_000)))))' > input-data.txt
```

## The final program

We need `conduit`, `bytestring` and `text` dependencies.

```haskell
executable haskell-conduit-example
    import:           warnings
    main-is:          Main.hs
    build-depends:    base ^>=4.14.3.0,
                      conduit ^>=1.3.4.2,
                      bytestring ^>=0.11.3.1,
                      text ^>=1.2.5
    hs-source-dirs:   app
    default-language: Haskell2010
```

The final example app can look something like this.

```haskell
{-# LANGUAGE OverloadedStrings #-}

module Main where

import Conduit
import qualified Data.Conduit.Combinators as CC
import qualified Data.ByteString as BS
import Data.Text                  as T
import Data.Text.Encoding         as T

byteStringToInt :: BS.ByteString -> Int
byteStringToInt = read . T.unpack . T.decodeUtf8

intToByteString :: Int -> BS.ByteString
intToByteString = T.encodeUtf8 . T.pack . show

main :: IO ()
main = runConduitRes $
  sourceFile "input-data.txt"
  .| CC.linesUnboundedAscii
  .| mapC byteStringToInt
  .| mapC (* 2)
  .| mapC ((<> "\n") . intToByteString)
  .| sinkFile "output-data.txt"
```
